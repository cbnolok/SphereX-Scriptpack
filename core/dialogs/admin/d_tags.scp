//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereServer.net
//****************************************************************************
// Save in ANSI encoding
//****************************************************************************
// New Multilanguage Admin
// Created by Golfin (http://eranova.cz/)
//****************************************************************************
// Last file update: 2.10.2020
//
VERSION=X

[DIALOG d_SphereplayerTags]
50,50
Src.Dialogclose d_SphereplayerTags

Page 0
Resizepic 0 0 30546 650 507
Gumppictiled 5 5 640 497 2604
Gumppictiled 5 5 640 25 30550
Gumppictiled 5 35 145 440 30550
Gumppictiled 155 35 490 440 30550
Gumppictiled 5 480 640 22 30550
Checkertrans 5 5 640 497

DText 10 7 0c1 SphereServer <Src.Version> Admin Panel (<Serv.Servname>) - <Def.<SRC.CTAG0.accountlang>_Tags_for_Character> <Name>
Button 615 5 4017 4018 1 0 0
Button 585 5 4008 4009 1 0 1

DText 10 37 0c1 <Def.<SRC.CTAG0.accountlang>_Account>

DText 40 62 0480 <Def.<SRC.CTAG0.accountlang>_Info_Account>
Button 5 60 4005 4006 1 0 2

DText 40 87 0480 <Def.<SRC.CTAG0.accountlang>_Action_Acc>
Button 5 85 4005 4006 1 0 3

DText 40 112 0480 <Def.<SRC.CTAG0.accountlang>_Notes>
Button 5 110 4005 4006 1 0 4

DText 10 142 0c1 <Def.<SRC.CTAG0.accountlang>_Character>

DText 40 167 0480 <Def.<SRC.CTAG0.accountlang>_Properties>
Button 5 165 4005 4006 1 0 5

DText 40 192 0480 <Def.<SRC.CTAG0.accountlang>_Skills>
Button 5 190 4005 4006 1 0 6

DText 40 217 0480 <Def.<SRC.CTAG0.accountlang>_Flags>
Button 5 215 4005 4006 1 0 7

DText 40 242 0480 <Def.<SRC.CTAG0.accountlang>_Tags>
Button 5 240 4005 4006 1 0 8

DText 40 267 0480 <Def.<SRC.CTAG0.accountlang>_Events>
Button 5 265 4005 4006 1 0 9

Page 1
DText 160 35 0c1 <Def.<SRC.CTAG0.accountlang>_Del_Tags>
If (0<TagCount>)
   Local.Page=1
   Local.oy=60
   Page 1
   For x 0 <Eval <TagCount>-1>
      If (<Local.oy> > 450)
         Local.oy = 60
         Local.Page += 1
         Button 615 480 4005 4006 0 <Eval <Local.Page>> 0 // Forward Button
         Page <Eval <Local.Page>>
         Button 150 480 4014 4015 0 <Eval <Local.Page> - 1> 0 // Back Button
      Endif
      DText 195 <Local.oy> 0480 Tag.<Tagat.<Local.x>.key> = <Eval <Tagat.<Local.x>.val>>
      If (<Src.Account.Plevel> > 3)
         Button 160 <Local.oy> 4017 4018 1 0 <Eval (1000 + <Local.x>)>
      Endif
      Local.oy += 25
   end
Else
   DText 160 60 0c1 <Def.<SRC.CTAG0.accountlang>_No_Tags>
Endif

[DIALOG d_SphereplayerTags Button]
On=0 1
   Src.Dialog d_SpherePlayerTweak

On=2
   Dialog d_SpherePlayerTweak 1

On=3
   Dialog d_SpherePlayerTweak 2

On=4
   Dialog d_SpherePlayerNotes

On=5
   Dialog d_SpherePlayerTweak 3

On=6
   Dialog d_SpherePlayerTweak 4

On=7
   Dialog d_SpherePlayerTweak 5

On=8
   Dialog d_SphereplayerTags

On=9
   Dialog d_SpherePlayerTweak 6

On=1000,1999
	Tag.<Tagat.<Eval <ArgN>-1000>.Key>=
	Dialog d_SphereplayerTags
	Src.Sysmessage @tcolor_Orange <Def.<SRC.CTAG0.accountlang>_Tags_Sys> <Name> <Def.<SRC.CTAG0.accountlang>_Updated_Sys>

[EOF]
