//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

[FUNCTION f_isHuman]
IF (<BODY>==c_man) || (<BODY>==c_woman) || (<BODY>==c_ghost_man) || (<BODY>==c_ghost_woman) || (<BODY>==c_man_gm)
	RETURN 1
ENDIF
RETURN 0

[FUNCTION f_isElf]
IF (<BODY>==c_elf_male) || (<BODY>==c_elf_female) || (<BODY>==c_elf_ghost_man) || (<BODY>==c_elf_ghost_woman)
	RETURN 1
ENDIF
RETURN 0

[FUNCTION f_isGargoyle]
IF (<BODY>==c_gargoyle_male) || (<BODY>==c_gargoyle_female) || (<BODY>==c_garg_ghost_man) || (<BODY>==c_garg_ghost_woman)
	RETURN 1
ENDIF
RETURN 0
//[FUNCTION f_isGarg]	// backwards compatibility
//RETURN f_isGargoyle

[FUNCTION f_isFemale]
if (<serv.chardef.<obody>.can>&mt_female)	//Checking on chardef directly here because if you change your body your Can flags won't be updated since they are now stored in the char itself when it creates.
	return 1
endif
return 0

[FUNCTION f_isPlayableChar]
//IF <IsHuman> || <IsElf> || <IsGargoyle>
IF (<f_isHuman> || <f_isElf> || <f_isGargoyle>) //IsHuman, IsElf and IsGargoyle is not valid command so changed it to scripted ones!
	RETURN 1
ENDIF
RETURN 0

[FUNCTION f_isUndead]
IF (<ISEVENT.e_undead>) || (<ISTEVENT.e_undead>) // || (<NPC>==brain_undead)
	RETURN 1
ENDIF
RETURN 0

//<ITEM>.f_isStackable
[FUNCTION f_isStackable]
RETURN <qVal (<CAN>&can_i_pile)?1:0>

//<NPC>.f_isTameable
[FUNCTION f_isTameable]
RETURN <qVal (((<NPC> == brain_animal) || (<NPC> == brain_monster) || (<NPC> == brain_dragon)) && (<dTAMING>)) ? 1 : 0>

[EOF]
